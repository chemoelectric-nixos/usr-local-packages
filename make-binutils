#!/bin/zsh

if [[ $# -ne 3 ]]; then
    echo "Usage: $0 PACKAGE_VERSION TARGETED_HOST CHECK"
    exit 2
fi

name=binutils

export LIBRARY_PATH=/usr/local/lib:/usr/local/lib64

# Use static libraries as much as possible, because the toolchain gets
# tricky otherwise. We will build our static libraries with
# position-independent code.
static_libraries=`mktemp -d`
ln -s /usr/local/lib/libzstd.a ${static_libraries}

environment_variables=( )
configure_arguments=(
    --enable-default-pie
    --disable-multilib
    --disable-nls
    --disable-canonical-system-headers
    --enable-year2038
    --enable-lto
    --enable-host-shared
    --enable-{gcov,gprof,gprofng}
    --enable-threads
    --disable-werror
    --enable-{rpath,new-dtags}
    --enable-install-libiberty
    --with-zstd-include=/usr/local
    --with-zstd-lib=${static_libraries}
    --without-{target-,}system-zlib
    --with-native-system-header-dir=/usr/local/include
)

source shared/basic.zsh

#
# Modify linker scripts. Rid them of library directories outside the
# /usr/local.
#
sed -i -E 's:SEARCH_DIR\("(/usr)?/lib[^[:space:]]*"\);::g' \
    "${abs_destdir}"/usr/local/*/lib/ldscripts/* || ${bail_out}

#
# Remake the tarball.
#
rm "${bin_tarball}" || ${bail_out}
${tar} -cvaf "${bin_tarball}" -C "${abs_destdir}" usr || ${bail_out}

rm -R -f ${static_libraries}
