#!/bin/zsh

if [[ $# -ne 3 ]] && [[ $# -ne 4 ]]; then
    echo "Usage: $0 PACKAGE_VERSION TARGETED_HOST LANGUAGES [FIXED_POINT=no]"
    exit 2
fi

name=gcc

if [[ `echo "${1}" | sed 's/\..*//'` -le 11 ]]; then
    echo "This script is for gcc >= 12"
    exit 3
fi

ban_check=yes

fixed_point=no
if [[ $# -eq 4 ]]; then
    fixed_point="${4}"
fi

# Use static libraries as much as possible, because the toolchain gets
# tricky otherwise. We will build our static libraries with
# position-independent code.
static_libraries=`mktemp -d`
the_libraries=( gmp mpfr mpc isl zstd gc )
for i in ${the_libraries}; do
    echo "GROUP ( `echo "${the_libraries}" | sed -E 's|([^[:space:]]+)|/usr/local/lib/lib\1.a |g'`)" \
         > ${static_libraries}/lib${i}.so
done

# Rust, for instance, might expect cc.
extra_bin=`mktemp -d`
cat > "${extra_bin}"/cc <<EOF
#!/bin/zsh
exec gcc "\${@}"
EOF
chmod +x "${extra_bin}"/cc

export LIBRARY_PATH=${static_libraries}:/usr/local/lib:/usr/local/lib64
export PATH="${extra_bin}:${PATH}"

function patch_function
{
    sed -i 's|BUILD_LIBS = \$(BUILD_LIBIBERTY)|BUILD_LIBS = $(BUILD_LIBIBERTY) -liconv|' \
        gcc/Makefile.in
}

environment_variables=( )
configure_arguments=(
    -C
    --enable-languages="${3}"
    --enable-default-pie
    --disable-canonical-system-headers
    --disable-multilib
    --disable-nls
    --enable-year2038
    --enable-bootstrap
    --disable-pgo-build
    --enable-lto
    --disable-host-shared
    --enable-objc-gc
    --disable-werror
    --enable-gcov
    --enable-threads
    --enable-decimal-float
    --enable-{rpath,new-dtags}
    --enable-fixed-point="${fixed_point}"
    --with-{gmp,mpfr,mpc,isl,zstd,target-bdw-gc}-include=/usr/include
    --with-{gmp,mpfr,mpc,isl,zstd,target-bdw-gc}-lib=${static_libraries}
    --with-libiconv-prefix=/usr/local
    --with-libiconv-type=shared
    --without-{target-,}system-zlib
    --with-native-system-header-dir=/usr/local/include
)

source shared/basic.zsh

#
# Remove temporary directories from .la files.
#
find "${abs_destdir}" -name '*.la' |
    xargs sed -i -E "s:-L${static_libraries}::g" \
        || ${bail_out} 

#
# Make cc a synonym for gcc.
#
(cd "${abs_destdir}"/usr/local/bin && ln -s gcc cc || ${bail_out}) \
    || ${bail_out}

#
# Remake the tarball.
#
rm "${bin_tarball}" || ${bail_out}
${tar} -cvaf "${bin_tarball}" -C "${abs_destdir}" usr || ${bail_out}

rm -R -f ${static_libraries} ${extra_bin}
