#!/bin/zsh

if [[ $# -ne 3 ]] && [[ $# -ne 4 ]]; then
    echo "Usage: $0 PACKAGE_VERSION TARGETED_HOST LANGUAGES [FIXED_POINT=no]"
    exit 2
fi

name=gcc

if [[ `echo "${1}" | sed 's/\..*//'` -le 11 ]]; then
    echo "This script is for gcc >= 12"
    exit 3
fi

_languages="${3}"
if [[ "${_languages}" = "without-rust" ]]; then
    case "${1}" in
        15.* )
            _languages='c,ada,cobol,c++,d,fortran,go,lto,m2,objc,obj-c++'
            ;;
        *)
            echo "I do not know how to handle ‘without-rust’ for this compiler."
            ;;
    esac
fi

ban_check=yes

fixed_point=no
if [[ $# -eq 4 ]]; then
    fixed_point="${4}"
fi

# Use static libraries as much as possible, because the toolchain gets
# tricky otherwise. We will build our static libraries with
# position-independent code.
static_libraries=`mktemp -d`
ln -s /usr/local/include ${static_libraries}
install -d ${static_libraries}/lib
the_libraries=( gmp mpfr mpc isl zstd z gc )
for i in ${the_libraries}; do
    (
        echo "GROUP ( "
        echo "${the_libraries}" | sed -E 's|([^[:space:]]+)|/usr/local/lib/lib\1.a |g'
        echo ")"
    ) > ${static_libraries}/lib/lib${i}.so
    echo \'${static_libraries}/lib/lib${i}.so\' '->' \'`cat ${static_libraries}/lib/lib${i}.so`\'
done
ln -sv /usr/local/lib/crt?.o ${static_libraries}/lib

# Put /usr/local/lib64 in the path, in case I am using my older tools
# to rebuild GCC.
export LIBRARY_PATH="${static_libraries}/lib:/usr/local/lib:/usr/local/lib64"

extra_bin=`mktemp -d`

# Rust, for instance, might expect cc.
cat > "${extra_bin}"/cc <<EOF
#!/bin/zsh
exec gcc "\${@}"
EOF
chmod +x "${extra_bin}"/cc

export PATH="${extra_bin}:${PATH}"

function patch_function
{
    # This is where we teach gcc to put stuff in lib instead of lib64.
    sed -i \
        -e 's|m64=\.\./lib64\$|m64=$|' \
        -e 's|m32=\$|m32=../i686-linux-gnu|' \
        gcc/config/i386/t-linux64
}

environment_variables=( )
configure_arguments=(
    -C

    --prefix=/usr/local
    --libdir=/usr/local/lib
    --with-native-system-header-dir=/usr/local/include

    --enable-languages="${_languages}"
    --enable-default-pie
    --enable-host-pie
    --disable-fixincludes
    --disable-canonical-system-headers
    --disable-multilib
    --disable-nls
    --enable-year2038
    --enable-bootstrap
    --disable-pgo-build
    --enable-lto
    --disable-host-shared
    --enable-objc-gc
    --disable-werror
    --enable-gcov
    --enable-threads
    --enable-decimal-float
    --enable-new-dtags
    --enable-fixed-point="${fixed_point}"
    --with-{gmp,mpfr,mpc,isl,zstd,target-bdw-gc}=${static_libraries}
    --without-{target-,}system-zlib
)

source shared/basic.zsh

#
# Remove temporary directories from .la files.
#
find "${abs_destdir}" -name '*.la' |
    xargs sed -i -E "s:-L${static_libraries}::g" \
        || ${bail_out} 

#
# Make cc a synonym for gcc.
#
(cd "${abs_destdir}"/usr/local/bin && ln -s gcc cc || ${bail_out}) \
    || ${bail_out}

#
# Remake the tarball.
#
make_package "${bin_tarball}" "${abs_destdir}" || ${bail_out}

rm -R -f ${static_libraries} ${extra_bin}
