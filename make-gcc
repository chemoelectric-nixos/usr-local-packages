#!/bin/zsh

if [[ $# -ne 3 ]] && [[ $# -ne 4 ]]; then
    echo "Usage: $0 PACKAGE_VERSION TARGETED_HOST LANGUAGES [FIXED_POINT=no]"
    exit 2
fi

name=gcc

if [[ `echo "${1}" | sed 's/\..*//'` -le 11 ]]; then
    echo "This script is for gcc >= 12"
    exit 3
fi

ban_check=yes

fixed_point=no
if [[ $# -eq 4 ]]; then
    fixed_point="${4}"
fi

# Use static libraries as much as possible, because the toolchain gets
# tricky otherwise. We will build our static libraries with
# position-independent code.
static_libraries=`mktemp -d`
ln -s /usr/local/lib/lib{gmp,mpfr,mpc,isl,zstd,gc}.a ${static_libraries}

# Rust, for instance, might expect cc.
extra_bin=`mktemp -d`
cat > "${extra_bin}"/cc <<EOF
#!/bin/zsh
exec gcc "\${@}"
EOF
chmod +x "${extra_bin}"/cc

export LIBRARY_PATH=/usr/local/lib:/usr/local/lib64
export PATH="${extra_bin}:${PATH}"

environment_variables=( )
configure_arguments=(
    -C
    --enable-languages="${3}"
    --enable-default-pie
    --disable-canonical-system-headers
    --disable-multilib
    --disable-nls
    --enable-year2038
    --enable-bootstrap
    --disable-pgo-build
    --enable-lto
    --disable-host-shared
    --enable-objc-gc
    --disable-werror
    --enable-gcov
    --enable-threads
    --enable-decimal-float
    --enable-{rpath,new-dtags}
    --enable-fixed-point="${fixed_point}"
    --with-{gmp,mpfr,mpc,isl,zstd,target-bdw-gc}-include=/usr/local
    --with-{gmp,mpfr,mpc,isl,zstd,target-bdw-gc}-lib=${static_libraries}
    --without-{target-,}system-zlib
    --with-native-system-header-dir=/usr/local/include
)

source shared/basic.zsh

#
# Make cc a synonym for gcc.
#
(cd "${abs_destdir}"/usr/local/bin && ln -s gcc cc || ${bail_out}) \
    || ${bail_out}

#
# Remake the tarball.
#
rm "${bin_tarball}" || ${bail_out}
${tar} -cvaf "${bin_tarball}" -C "${abs_destdir}" usr || ${bail_out}

rm -R -f ${static_libraries} ${extra_bin}
