#!/bin/zsh

source shared/helpers.zsh
source shared/make_package.zsh
source shared/git.zsh

if [[ $# -ne 5 ]]; then
    echo "Usage: $0 PACKAGE_VERSION TARGETED_HOST CHECK ARCH TUNE"
    exit 2
fi

name=chibi-scheme

_package_version="${package_version:-${1?}}"
_targeted_host="${targeted_host:-${2?}}"
_check="${check:-${3?}}"
_arch="${4:?}"
_tune="${5:?}"

_build_name="${build_name:-___build___}"
_dest_name="${dest_name:-___dest___}"

bail_out=( exit 1 )

abs_src_tarball_dir=`realpath "${PWD}/src_tarballs"`
abs_bin_tarball_dir=`realpath "${PWD}/bin_tarballs"`
abs_srcdirs_dir=`realpath "${PWD}/srcdirs"`

abs_repo_dir="${abs_src_tarball_dir}/${name}.git"
git_check_out "${abs_repo_dir}" "${_package_version}"

packname="${name}-${_package_version}"
bin_tarball="${abs_bin_tarball_dir}/${packname}-binary-for-${_targeted_host}.tar.zst"
abs_srcdir="${abs_repo_dir}"
abs_builddir="${abs_srcdirs_dir}/${packname}/___build___"
abs_destdir="${abs_srcdirs_dir}/${packname}/___dest___"

rm -R -f "${abs_srcdirs_dir}/${packname}" || ${bail_out}
mkdir -p "${abs_builddir}" || ${bail_out}
(

    cd "${abs_builddir}" || ${bail_out}

    # Copy the sources.
    cp --archive --link --update=none "${abs_srcdir}"/* "${abs_builddir}" \
        || ${bail_out}

    sed -i -E \
        -e '1s|/bin/sh|/usr/bin/env sh|' \
        tools/snow-chibi || ${bail_out}

    _make_flags=( PREFIX=/usr/local CC="gcc -pipe -fPIC -march=${_arch} -mtune=${_tune}" )

    make all "${(@)_make_flags}" || ${bail_out}
    make libchibi-scheme.a "${(@)_make_flags}" || ${bail_out}
    make chibi-scheme-static "${(@)_make_flags}" || ${bail_out}

    make test-all "${(@)_make_flags}" || ${bail_out}

    make install DESTDIR="${abs_destdir}" "${(@)_make_flags}" || ${bail_out}
    install -m 755 chibi-scheme-static "${abs_destdir}"/usr/local/bin || ${bail_out}

) || ${bail_out}

make_package "${bin_tarball}" "${abs_destdir}" || ${bail_out}
