#!/bin/zsh

if [[ $# -ne 1 ]]; then
    echo "Usage: $0 REVISION"
    exit 3
fi

# Make ATS/Postiats from Git sources.
name=ATS-Postiats

_revision="${1}"
_targeted_host=x86_64-unknown-linux-gnu

tar=( tar --posix )
git=( git )
bail_out=( exit 1 )

abs_src_tarball_dir=`realpath "${PWD}/src_tarballs"`
abs_bin_tarball_dir=`realpath "${PWD}/bin_tarballs"`
abs_srcdirs_dir=`realpath "${PWD}/srcdirs"`
abs_files_dir=`realpath "${PWD}/files"`

abs_repo_dir="${abs_src_tarball_dir}/${name}.git"

function check_out_revision
{
    local revision="${1}"
    echo "Checking out revision ${revision}"
    (cd "${abs_repo_dir}" &&
         ${git} fetch &&
         ${git} checkout -q "${revision}") || ${bail_out}
}

check_out_revision "${_revision}"

packname="${name}-${_revision}"
bin_tarball="${abs_bin_tarball_dir}/${packname}-binary-for-${_targeted_host}.tar.xz"
abs_srcdir="${abs_repo_dir}"
abs_builddir="${abs_srcdirs_dir}/${packname}/«build»"
abs_destdir="${abs_srcdirs_dir}/${packname}/«dest»"

rm -R -f "${abs_srcdirs_dir}/${packname}" || ${bail_out}
install -d "${abs_builddir}" || ${bail_out}

(
    cd "${abs_builddir}" || ${bail_out}

    # Copy the sources.
    cp --archive --link --update=none "${abs_srcdir}"/* "${abs_builddir}" \
        || ${bail_out}

    export PATSHOME="$(realpath "${PWD}")"

    make -f codegen/Makefile_atslib || ${bail_out}
    make -f Makefile_devl all || ${bail_out}
    (cd contrib/ATS-extsolve-smt2 && make all) || ${bail_out}

    ul="${abs_destdir}/usr/local"
    ul_bin="${ul}/bin"
    ul_man1="${ul}/share/man/man1"
    ul_postiats="${ul}/lib/${name}-${_revision}"

    install -d "${ul_bin}" "${ul_postiats}" || ${bail_out}
    cp -R . "${ul_postiats}" || ${bail_out}
    for f in myatscc patscc patsopt; do
        sed -e 's|@PACKAGE_VERSION@|'"${_revision}"'|g' \
            -e 's|@PACKAGE_TARNAME@|'"${name}"'|g' \
            -e 's|@prefix@|/usr/local|g' \
            < "bin/${f}_env.sh.in" > "${ul_bin}/${f}" || ${bail_out}
        chmod a+x "${ul_bin}/${f}" || ${bail_out}
    done
    install contrib/ATS-extsolve-smt2/bin/* "${ul_bin}" || ${bail_out}

    #
    # Install manpages taken from Debian’s packaging.
    #
    install -d "${ul_man1}" || ${bail_out}
    install "${abs_files_dir}"/ATS-Postiats-manpages-from-Debian/*.1 \
            "${ul_man1}" || ${bail_out}
    
) || ${bail_out}

mkdir -p "${abs_bin_tarball_dir}" || ${bail_out}
${tar} -cvaf "${bin_tarball}" -C "${abs_destdir}" usr || ${bail_out}
