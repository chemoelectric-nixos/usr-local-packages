#!/bin/zsh

source shared/make_package.zsh
source shared/git.zsh

#
# If PREFIX be set, it is assumed to be under /usr/local
#
if [[ $# -lt 4 ]] || [[ $# -gt 7 ]]; then
    echo "Usage: $0 REVISION TARGETED_HOST ARCH TUNE [PROGRAM_SUFFIX] [CHICKEN_BOOT] [PREFIX]"
    exit 3
fi

# Make CHICKEN Scheme from the Git sources.
name=chicken-core

_revision="${1?}"
_targeted_host="${2?}"
_arch="${3?}"
_tune="${4?}"
_program_suffix="${5}"
_chicken_boot="${6}"
_prefix="${7:-/usr/local}"

bail_out=( exit 1 )

abs_src_tarball_dir=`realpath "${PWD}/src_tarballs"`
abs_bin_tarball_dir=`realpath "${PWD}/bin_tarballs"`
abs_srcdirs_dir=`realpath "${PWD}/srcdirs"`

abs_repo_dir="${abs_src_tarball_dir}/${name}.git"
git_check_out "${abs_repo_dir}" "${_revision}"

packname="${name}-${_revision}"
bin_tarball="${abs_bin_tarball_dir}/${packname}-binary-for-${_targeted_host}.tar.zst"
abs_srcdir="${abs_repo_dir}"
abs_builddir="${abs_srcdirs_dir}/${packname}/___build___"
abs_destdir="${abs_srcdirs_dir}/${packname}/___dest___"

rm -R -f "${abs_srcdirs_dir}/${packname}" || ${bail_out}
install -d "${abs_builddir}" || ${bail_out}
(
    cd "${abs_builddir}" || ${bail_out}

    # Out-of-source builds are not actually supported yet, as of
    # 2025-10-04. So copy the sources.
    cp --archive --link --update=none "${abs_srcdir}"/* "${abs_builddir}" \
        || ${bail_out}

    if [[ -n "${_chicken_boot}" ]]; then
        chicken_boot_arguments="--chicken=${_chicken_boot}"
    else
        chicken_boot_arguments=""
    fi

    env CFLAGS="-pipe -fPIC -O2 -march=""${_arch}"" -mtune=""${_tune}" ./configure \
        --prefix="${_prefix}" \
        --platform=linux \
        --program-suffix="${_program_suffix}" \
        ${chicken_boot_arguments}

    make || ${bail_out}
    make check || ${bail_out}
    make install DESTDIR="${abs_destdir}" || ${bail_out}

) || ${bail_out}

for f in `find ${abs_destdir} -xtype f`; do
    if [[ -x "${f}" ]]; then
        patchelf --add-rpath "${_prefix}"/lib "${f}" 2> /dev/null || true
    fi
done

make_package "${bin_tarball}" "${abs_destdir}" || ${bail_out}

