#!/bin/zsh

source shared/make_package.zsh

if [[ $# -ne 4 ]]; then
    echo "Usage: $0 PACKAGE_VERSION TARGETED_HOST ARCH TUNE"
    exit 2
fi

name=bzip2

abs_src_tarball_dir=`realpath "${PWD}/src_tarballs"`
abs_bin_tarball_dir=`realpath "${PWD}/bin_tarballs"`
abs_srcdirs_dir=`realpath "${PWD}/srcdirs"`

targeted_host="${2}"
check=no
version="${1}"
packname="${name}-${version}"
bin_tarball="${abs_bin_tarball_dir}/${packname}-binary-for-${targeted_host}.tar.zst"
abs_srcdir="${abs_srcdirs_dir}/${packname}"
abs_destdir="${abs_srcdir}/Â«destÂ»"
bail_out=( exit 1 )

src_tarball="${abs_src_tarball_dir}/${packname}.tar.gz"

make_arguments=( CC="gcc -fPIC -march=${3} -mtune=${4}" )

mkdir -p "${abs_srcdirs_dir}"
rm -R -f "${abs_srcdirs_dir}/${packname}" || ${bail_out}
tar -f "${src_tarball}" -C "${abs_srcdirs_dir}" -x || ${bail_out}
(
    cd "${abs_srcdir}" || ${bail_out}
    make ${(@)make_arguments} || ${bail_out}
    make install ${(@)make_arguments} PREFIX="${abs_destdir}"/usr/local \
        || ${bail_out}
    make -f Makefile-libbz2_so ${(@)make_arguments} || ${bail_out}
    install -d "${abs_destdir}"/usr/local/share || ${bail_out}
    mv "${abs_destdir}"/usr/local/{man,share} || ${bail_out}
    ln -s libbz2.so.${version} libbz2.so || ${bail_out}
    install libbz2.so* "${abs_destdir}"/usr/local/lib || ${bail_out}
    install -d "${abs_destdir}"/usr/local/share/doc/"${name}" \
        || ${bail_out}
    install manual.{pdf,html} \
            "${abs_destdir}"/usr/local/share/doc/"${name}" \
        || ${bail_out}

    #
    # Try to ensure ELF files have /usr/local/lib in the RUNPATH.
    # Also clean out any /tmp entries.
    #
    for f in `find ${abs_destdir}/usr/local -xtype f`; do
        if [[ -x "${f}" ]]; then
            patchelf --add-rpath /usr/local/lib "${f}" 2> /dev/null
            patchelf --shrink-rpath \
                     --allowed-rpath-prefixes /usr/local:/nix/store \
                     "${f}" 2> /dev/null
            true
        fi
    done

) || ${bail_out}

make_package "${bin_tarball}" "${abs_destdir}" || ${bail_out}
